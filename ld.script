/*    PhoeniX OS Linking script
**    Copyright (C) 2013  PhoeniX
**
**    This program is free software: you can redistribute it and/or modify
**    it under the terms of the GNU General Public License as published by
**    the Free Software Foundation, either version 3 of the License, or
**    (at your option) any later version.
**
**    This program is distributed in the hope that it will be useful,
**    but WITHOUT ANY WARRANTY; without even the implied warranty of
**    MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
**    GNU General Public License for more details.
**
**    You should have received a copy of the GNU General Public License
**    along with this program.  If not, see <http://www.gnu.org/licenses/>.
*/

OUTPUT_FORMAT(elf64-x86-64)
ENTRY(_efi_start)
SECTIONS
{
    __stack_start__ = 0x1000;
    __stack_end__ = 0x2000;
    __first_page__ = 0x2000;
    __pagetable__ = 0x20000;
    .text 0x401000 : ALIGN(1) {
        __text_start__ = . ;
        *(.text32)
        *(.init)
        
        . = ALIGN(64);
        
        __init_start__ = . ;
        *(.init_array*)
        __init_end__ = . ;
        
        *(.text .text.*)
        *(.gnu.linkonce.t.*)
        
        . = ALIGN(8);
        
        ___CTOR_LIST__ = .; __CTOR_LIST__ = . ;
        LONG (-1); LONG (-1); *(.ctors); *(.ctor); *(SORT(.ctors.*));  LONG (0); LONG (0);
        ___DTOR_LIST__ = .; __DTOR_LIST__ = . ;
        LONG (-1); LONG (-1); *(.dtors); *(.dtor); *(SORT(.dtors.*));  LONG (0); LONG (0);
        PROVIDE (etext = .);
        *(.fini)
        . = ALIGN(64);
        __text_end__ = . ;
    }
    .data BLOCK(1) : ALIGN(1) {
        __data_start__ = . ;
        *(.rodata .rodata.*)
        *(.data .data.*)
        *(.got.plt)
        __data_end__ = . ;

        __modules_start__ = . ;
        *(.modules)
        . = ALIGN(64);
        __modules_end__ = . ;
    }
    .bss BLOCK(1) : ALIGN(1) {
        __bss_start__ = . ;
        *(.bss .bss.*)
        *(COMMON)
        __bss_end__ = . ;
    }
    /DISCARD/ : { *(.comment) *(.eh_frame) }
}
